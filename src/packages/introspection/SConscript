from glob_files import globFiles
import os

Import('globalEnv')

envPackage = globalEnv.Clone()
staticBuild = globalEnv['with_static']

if not staticBuild:
    globalEnv.targetManager.targets['dscript'].applyBuild(envPackage)

pathPackage = envPackage.subst('${libdir}/dragonscript/Introspection')
#pathPackageTests = envPackage.subst('${libdir}/dragonscript/IntrospectionTests')
internalPackage = envPackage['with_internal_packages']

# setup the builders
targetPackage = globalEnv.targetManager.TargetLibrary('Package Introspection')
globalEnv.targetManager.add('pak_introspection', targetPackage)

sources = []
headers = []
scripts = []
#tests = []
globFiles(envPackage, 'src', '*.cpp', sources)
globFiles(envPackage, 'src', '*.h', headers)
globFiles(envPackage, 'src', '*.ds', scripts)
#globFiles(envPackage, 'tests', '*.ds', tests)

objects = [envPackage.SharedObject(s) for s in sources]

if not staticBuild:
    if internalPackage:
        library = envPackage.StaticLibrary(target='pakintrospection', source=objects)
    else:
        library = envPackage.SharedLibrary(target='Introspection', source=objects, SHLIBPREFIX='')
    targetPackage.build.extend(library)
targetPackage.aliasBuild(envPackage, 'pak_introspection_build')

targetPackage.installCutDirs(envPackage, pathPackage, scripts, 1)
#targetPackage.installCutDirs(envPackage, pathPackageTests, tests, 1)
if not internalPackage and not staticBuild:
    targetPackage.install.extend(envPackage.Install(pathPackage, library[0])) # only first target in case clutter exists
targetPackage.aliasInstall(envPackage, 'pak_introspection')

targetPackage.archiveCutDirs(envPackage, pathPackage, scripts, 1)
#targetPackage.archiveCutDirs(envPackage, pathPackageTests, tests, 1)
if not internalPackage and not staticBuild:
    targetPackage.archiveLibrary(envPackage, pathPackage, library[0:1])

if internalPackage:
    if staticBuild:
        globalEnv.targetManager.targets['internalPackages'].target.extend(objects)
    else:
        globalEnv.targetManager.targets['dscript'].specials['addInternalPackage'](envPackage, library)
